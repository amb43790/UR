%right OR
%right AND
%left MINUS

%%

expr: condition                   { $_[1] }
    | expr AND expr               { [ @{$_[1]}, @{$_[3]} ] }
    | expr OR expr                { [ [$_[1]], [$_[3]] ] }
    | LEFT_PAREN expr RIGHT_PAREN { $_[2] }
    ;

condition: property operator value                 { [ "$_[1] $_[2]" => $_[3] ] }
         | property like_operator LIKE_PATTERN     { [ "$_[1] $_[2]" => $_[3] ] }
         | property in_operator in_value           { print "Matches in_operator\n";[ "$_[1] $_[2]" => $_[3] ] }
         | property COLON in_value                 { print "matches colon in operator\n"; [ "$_[1] in" => $_[3] ] }
         | property negation COLON in_value        { print "matches negation colon in\n";[ "$_[1] $_[2] in" => $_[4] ] }
         | property between_operator between_value { print "matches between_operator\n"; [ "$_[1] $_[2]" => $_[3] ] }
         | property COLON between_value            { print "matches colon between\n";[ "$_[1] between" => $_[3] ] }
         | property negation COLON between_value   { print "matches netation colon between\n"; [ "$_[1] $_[2] between" => $_[4] ] }
         ;

property:   IDENTIFIER { $_[1] }
        ;

operator: an_operator           { $_[1] }
        | negation an_operator  { "$_[1] $_[2]" }
        ;

negation: NOT_WORD { 'not' }
        | NOT_BANG { 'not' }
        ;

an_operator: OPERATORS { $_[1] }
           | EQUAL_SIGN { '=' }
           | DOUBLEEQUAL_SIGN { '=' }
        ;

like_operator: LIKE_WORD { 'like' }
             | negation LIKE_WORD { "$_[1] like" }
             | TILDE { 'like' }
             | negation TILDE { "$_[1] like" }
             ;

in_operator: IN_WORD { 'in' }
           | negation IN_WORD { "$_[1] in" }
           ;

in_value: set  { $_[1] }
        | value IN_DIVIDER in_value { [ $_[1], @{$_[3]} ] }
        | value IN_DIVIDER value { [ $_[1], $_[3] ] }
        ; 

set: LEFT_BRACKET set_body RIGHT_BRACKET { $_[2] }
   ;

set_body: value COMMA set_body { [ $_[1], @{$_[3]} ] }
        | value { [ $_[1] ] }
        ;

between_operator: BETWEEN_WORD { 'between' }
                | negation BETWEEN_WORD { "$_[1] between" }
                ;

between_value: value MINUS value { [ $_[1], $_[3] ] }
             ;
   
keyword_as_value: AND { $_[1] }
                | OR  { $_[1] }
                | IN_WORD { $_[1] }
                | LIKE_WORD { $_[1] }
                | BETWEEN_WORD { $_[1] }
                | NOT_WORD { $_[1] }
       ;

value: IDENTIFIER { $_[1] }
     | number { $_[1] }
     | WORD   { $_[1] }
     | DOUBLEQUOTE_STRING { ($_[1] =~ m/^"(.*?)"$/)[0]; }
     | SINGLEQUOTE_STRING { ($_[1] =~ m/^'(.*?)'$/)[0]; }
     | keyword_as_value { $_[1] }
     ;

number: INTEGER { $_[1] + 0 }
      | REAL    { $_[1] + 0 }
      | MINUS INTEGER { 0 - $_[2] } # to reject --5
      | MINUS REAL { 0 - $_[2] }
      ; 

%%

package UR::BoolExpr::BxParser;

use strict;
use warnings;

sub _error {
    my @expect = $_[0]->YYExpect;
    my $tok = $_[0]->YYData->{INPUT};
    my $err = "Syntax error near '$tok'";
    my $rem = $_[0]->YYData->{REMAINING};
    $err .= ", remaining text: '$rem'" if $rem;
    $err .= "\nExpected one of: " . join(", ", @expect) . "\n";
    Carp::croak($err);
}

my @tokens = (
    AND => qr{and|,},
    OR => qr{or},
    BETWEEN_WORD => qr{between},
    LIKE_WORD => qr{like},
    IN_WORD => qr{in},
    IDENTIFIER => qr{[a-zA-Z_][a-zA-Z0-9_.]*},
    MINUS => qr{-},
    INTEGER => qr{\d+},
    REAL => qr{\d*\.\d+|\d+\.\d*},
    WORD => qr{\w+},
    DOUBLEQUOTE_STRING => qr{"(\\.|[^"])+"},
    SINGLEQUOTE_STRING => qr{'(\\.|[^'])+'},
    LEFT_PAREN => qr{\(},
    RIGHT_PAREN => qr{\)},
    LEFT_BRACKET => qr{\[},
    RIGHT_BRACKET => qr{\]},
    COMMA => qr{,},
    NOT_WORD => qr{not},
    NOT_BANG => qr{!},
    EQUAL_SIGN => qr{=},
    DOUBLEEQUAL_SIGN => qr{=>},
    OPERATORS => qr{<|>|<=|>=},
    COLON => qr{:},
    TILDE => qr{~},
    LIKE_PATTERN => qr{[\w%_]+},
    IN_DIVIDER => qr{\/},
);

sub parse {
    my $string = shift;
    my %params = @_;

    my $debug = $params{'debug'};

    print "\nStarting parse for string $string\n" if $debug;
    my $parser = BxParser->new();

    my $get_next_token = sub {
        if (length($string) == 0) {
            print "String is empty, we're done!\n" if $debug;
            return (undef, '');  
       }

        my $longest = 0;
        my $longest_token = '';
        my $longest_match = '';

        for(my $i = 0; $i < @tokens; $i += 2) {
            my($tok, $regex) = @tokens[$i, $i+1];
            print "Trying token $tok... " if $debug;

            if ($string =~ m/^(\s*($regex)\s*)/) {
                print "Matched >>$1<<" if $debug;
                my $match_len = length($1);
                if ($match_len > $longest) {
                    print "\n  ** It's now the longest" if $debug;
                    $longest = $match_len;
                    $longest_token = $tok;
                    $longest_match = $2;
                }
            }
            print "\n" if $debug;
        }

        $string = substr($string, $longest);
        print "Consuming up to char pos $longest chars, string is now >>$string<<\n" if $debug;
        $parser->YYData->{REMAINING} = $string;
        if ($longest) {
            $parser->YYData->{INPUT} = $longest_token;
            print "Returning token $longest_token, match $longest_match\n" if $debug;
            return ($longest_token, $longest_match);
        }
        print "Didn't match anything, done!\n" if $debug;
        return (undef, '');  # Didn't match anything
    };

    return $parser->YYParse(
               yylex => $get_next_token,
               yyerror => \&_error,
               yydebug => 0,
           );
}

